;;; version 0.2.3

(defmacro cond [*body]
  (def recur [*body]
    (if (< (len body) 4)
        (return `(if ~@body))
        (do (= [test then *orelse] body)
            (return `(if ~test ~then ~(recur *orelse))))))
  (return (recur *body)))

(defmacro conde [*body]
  (def recur [*body]
    (if (< (len body) 4)
        (return `(ife ~@body))
        (do (= [test then *orelse] body)
            (return `(ife ~test ~then ~(recur *orelse))))))
  (return (recur *body)))

(defmacro fn [*body]
  (return `(lambda ~@body)))

(from functools [reduce])

(def cc-next [pos]
  (return [[pos (/ 14 16)]
           [0 (/ 1 16)]
           [10 (/ 1 16)]]))

(def ch-next [pos]
  (= nr (conde (== pos 7)
               15
               (== pos 22)
               25
               5))
  (= nu (conde (== pos 7)
               12
               (== pos 22)
               28
               12))
  (return [[pos (/ 6 16)]
           [0 (/ 1 16)]
           [10 (/ 1 16)]
           [11 (/ 1 16)]
           [24 (/ 1 16)]
           [39 (/ 1 16)]
           [5 (/ 1 16)]
           [nr (/ 2 16)]
           [nu (/ 1 16)]
           [(- pos 3) (/ 1 16)]]))

(def solution [n]
  (= board (+ [1] (* [0] 39)))
  (= after-jail (* [0] 40))
  (= after-two-jail (* [0] 40))
  (= ccs {, 2 17 33})
  (= chs {, 7 22 36})
  (while True
    (= newboard (* [0] 40))
    (= new-after-jail (* [0] 40))
    (= new-after-two-jail (* [0] 40))
    (for [i ip] (enumerate board)
      (for d1 (range 1 (+ n 1))
        (for d2 (range 1 (+ n 1))
          (= sp (/ 1 n n))
          (= pos (% (+ i d1 d2) 40))
          ;; consecutive
          (if (== d1 d2)
              (do (= pspeed
                     (ife (and (!= i 10) (== d1 d2))
                          (+ (* (/ (- n 1) n n n)
                                (- 1
                                   (/ (+ (sub after-jail i)
                                         (sub after-two-jail i))
                                      ip)))
                             (* (/ 1 n n)
                                (/ (sub after-two-jail i)
                                   ip)))
                          0))
                  (+= (sub newboard 10) (* ip sp pspeed))
                  (*= sp (- 1 pspeed))))
          (= next-pos (conde (== pos 30)
                             [[10 1]]

                             (in pos ccs)
                             (cc-next pos)

                             (in pos chs)
                             (ch-next pos)

                             [[pos 1]]))
          (for [pos pp] next-pos
            (+= (sub newboard pos) (* ip sp pp))
            (if (and (!= pos 10) (== i 10))
                (+= (sub new-after-jail pos) (* ip sp pp)))
            (if (and (!= pos 10) (sub after-jail i))
                (+= (sub new-after-two-jail pos)
                    (* (sub after-jail i) sp pp)))))))
    (if (== board newboard)
        (break)
        (do (= board newboard)
            (= after-jail new-after-jail)
            (= after-two-jail new-after-two-jail))))
  (return (sub (sorted (enumerate board)
                       :key (fn [x] (sub x 1))
                       :reverse True)
               [: _ 3])))

(defmacro euler [*inputs]
  (return `(do (from time [time])
               (def main []
                 (= start (time))
                 (print (solution ~@inputs))
                 (print "elapsed:" (- (time) start)))
               (main))))

(euler 4)
